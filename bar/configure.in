dnl --------------------------------------------------------------------
dnl
dnl $Source: /home/torsten/cvs/bar/configure.in,v $
dnl $Revision: 1.9 $
dnl $Author: torsten $
dnl Contents: BAR auto-configure script
dnl
dnl --------------------------------------------------------------------

dnl ----------------------- additional macros --------------------------

dnl --- check if C compiler long long data type available
# (original macro from http://ac-archive.sourceforge.net/C_Support/ac_c_long_long.html)
AC_DEFUN([AC_C_LONG_LONG],
[
  AC_CACHE_CHECK(
    [for long long int],
    ac_cv_c_long_long,
    [
      if test "$GCC" = yes; then
        ac_cv_c_long_long=yes
      else
        AC_TRY_COMPILE(,[long long int i;],
        ac_cv_c_long_long=yes,
        ac_cv_c_long_long=no)
      fi
    ]
  )
  AS_IF([test $ac_cv_c_long_long = yes],[$1],[$2])dnl
  AC_PROVIDE([$0])dnl
])

AC_DEFUN([AC_CHECK_LARGEFILE_DEFINES],
[
  AC_CACHE_CHECK(
    [for needed large files defines],
    ac_largefile_defines,
    [
      cat << \EOF > conftest.c
#include <stdlib.h>
#include <stdio.h>
#include <sys/types.h>
#include <unistd.h>
int main(void) { printf("%p",fseeko64); return 0; }
EOF
      ${CC} ${CFLAGS} -Wall -Werror -c conftest.c -o conftest.o 1>/dev/null 2>/dev/null; rc=$?
      if test $rc -eq 0; then
        ac_largefile_defines=""
      else
        ${CC} ${CFLAGS} -Wall -Werror -Dz_LARGEFILE64_SOURCE -c conftest.c -o conftest.o 1>/dev/null 2>/dev/null; rc=$?
        if test $rc -eq 0; then
          ac_largefile_defines="_LARGEFILE64_SOURCE"
        else
          ac_largefile_defines=""
        fi
      fi
      rm -f conftest.c conftest.o
      CC_DEFINES="$CC_DEFINES $ac_largefile_defines"
    ]
  )dnl
  AC_PROVIDE([$0])dnl
])

AC_DEFUN([AC_CHECK_PTHREAD_DEFINES],
[
  AC_CACHE_CHECK(
    [for needed pthread defines],
    ac_extended_pthread_defines,
    [
      cat << \EOF > conftest.c
#include <stdlib.h>
#include <stdio.h>
#include <pthread.h>
int main(void) { printf("%p",pthread_mutexattr_settype); return 0; }
EOF
      ${CC} ${CFLAGS} -Wall -Werror -c conftest.c -o conftest.o 1>/dev/null 2>/dev/null; rc=$?
      if test $rc -eq 0; then
        ac_extended_pthread_defines=""
      else
        ${CC} ${CFLAGS} -Wall -Werror -D_GNU_SOURCE -c conftest.c -o conftest.o 1>/dev/null 2>/dev/null; rc=$?
        if test $rc -eq 0; then
          ac_extended_pthread_defines="_GNU_SOURCE"
        else
          ac_extended_pthread_defines=""
        fi
      fi
      rm -f conftest.c conftest.o
      CC_DEFINES="$CC_DEFINES $ac_extended_pthread_defines"
    ]
  )dnl
  AC_PROVIDE([$0])dnl
])

dnl ------------------------- basic settings ---------------------------

dnl --- initialize autoconf
AC_INIT(.)
AC_PREREQ(2.53)

dnl --- set source-directory (specify existing source file)
AC_CONFIG_SRCDIR(bar.c)

dnl --- install-sh, config.sub and config.guess are in this directory.
AC_CONFIG_AUX_DIR(bin)

dnl --- check bost system
AC_CANONICAL_HOST

dnl AC_PRESERVE_HELP_ORDER

dnl ----------------------- substitute settings ------------------------

AC_SUBST(INSTALL_DIR)                       dnl installation directory
AC_SUBST(INSTALL_BIN_DIR)                   dnl installation directory for binaries
AC_SUBST(CONFIG_DIR)                        dnl configuration directory
AC_SUBST(TLS_DIR)                           dnl TLS/SSL directory

AC_SUBST(CC)
AC_SUBST(LD)
AC_SUBST(STRIP)

AC_SUBST(CC_FLAGS)
AC_SUBST(CC_WARN_FLAGS)
AC_SUBST(CC_OPTIMIZE)
AC_SUBST(CC_DEFINES)
AC_SUBST(CC_INCLUDES)

AC_SUBST(LD_FLAGS)

AC_SUBST(LIBRARY_PATHS)
AC_SUBST(LIBRARIES)
AC_SUBST(STATIC_LIBRARIES)

dnl ----------------------- initialize variables -----------------------

CC_FLAGS=""
CC_DEFINES=""
CC_OPTIMIZE=""
CC_INCLUDES=""

LD_FLAGS=""
LIBRARY_PATHS=""
LIBRARIES=""
STATIC_LIBRARIES=""

ENABLE_DEBUG="no"
ENABLE_LINK_DYNAMIC="no"
ENABLE_ZIP="yes"
ENABLE_BZIP2="yes"
ENABLE_GCRYPT="yes"
ENABLE_FTP="yes"
ENABLE_SSH="yes"
ENABLE_TLS="yes"

dnl ---------------------------- check system --------------------------

dnl ---------------------------- directories ---------------------------

dnl --------------------- --with/--without options ---------------------

AC_ARG_WITH(
  config-dir,
  AC_HELP_STRING([--with-config-dir=<path>],[configuration directory (default: /etc/bar)]),
  [CONFIG_DIR=$withval],
  [CONFIG_DIR=/etc/bar]
)
AC_ARG_WITH(
  tls-dir,
  AC_HELP_STRING([--with-tls-dir=<path>],[TLS/SLL configuration and certificate directory (default: /etc/ssl)]),
  [TLS_DIR=$withval],
  [TLS_DIR=/etc/ssl]
)

dnl ------------------- --enable/--disable options ---------------------

AC_ARG_ENABLE(
  debug,
  AC_HELP_STRING([--enable-debug],[enable debug version]),
  [ENABLE_DEBUG=$enableval]
)
AC_ARG_ENABLE(
  link-dynamic,
  AC_HELP_STRING([--enable-link-dynamic],[enable dynamic linkage]),
  [ENABLE_LINK_DYNAMIC=$enableval]
)
AC_ARG_ENABLE(
  zip,
  AC_HELP_STRING([--disable-zip],[disable zip support]),
  [ENABLE_ZIP=$enableval]
)
AC_ARG_ENABLE(
  bzip2,
  AC_HELP_STRING([--disable-bzip2],[disable bzip2 support]),
  [ENABLE_BZIP2=$enableval]
)
AC_ARG_ENABLE(
  gcrypt,
  AC_HELP_STRING([--disable-gcrypt],[disable gcrypt support]),
  [ENABLE_GCRYPT=$enableval]
)
AC_ARG_ENABLE(
  ftp,
  AC_HELP_STRING([--disable-ftp],[disable FTP support]),
  [ENABLE_FTP=$enableval]
)
AC_ARG_ENABLE(
  ssh,
  AC_HELP_STRING([--disable-ssh],[disable SSH support]),
  [ENABLE_SSH=$enableval]
)
AC_ARG_ENABLE(
  tls,
  AC_HELP_STRING([--disable-tls],[disable TLS support]),
  [ENABLE_TLS=$enableval]
)

if test $ENABLE_TLS = "yes" -a ! $ENABLE_GCRYPT = "yes"; then
  AC_MSG_WARN([gcrypt no enabled, disabled TLS support.])
  ENABLE_TLS=no
fi

dnl ------------------------------ programs ----------------------------

AC_PATH_PROGS(CC,gcc cc)
AC_PATH_PROGS(LD,ld gcc)
AC_PATH_PROGS(STRIP,strip)

AC_PATH_PROGS(PERL,perl)
if test -z "$PERL"; then
  AC_MSG_ERROR([Cannot find a 'perl' in PATH. Please check your PATH environment variable.])
fi
AC_PROG_INSTALL
if test -z "$INSTALL"; then
  AC_MSG_ERROR([Cannot find a 'install' in PATH. Please check your PATH environment variable.])
fi

CC_DEFINES="$CC_DEFINES"
if test "$ENABLE_DEBUG" = "yes"; then
  CC_OPTIMIZE="$CC_OPTIMIZE -O0"
else
  CC_OPTIMIZE="$CC_OPTIMIZE -O2"
  CC_DEFINES="$CC_DEFINES NDEBUG"
fi
CC_INCLUDES="$CC_INCLUDES"

LD_FLAGS=""
if test ! $ENABLE_LINK_DYNAMIC = "yes"; then
  LD_FLAGS="-static-libgcc"
fi
LIBRARY_PATHS="$LIBRARY_PATHS /usr/local/lib"

dnl -------------- headers/functions/definitions/libraries -------------

# set variables for tests
CFLAGS="$CC_FLAGS"
for z in $CC_DEFINES ""; do
  if test -n "$z"; then
    CFLAGS="$CFLAGS -D$z"
  fi
done
for z in $CC_INCLUDES ""; do
  if test -n "$z"; then
    CFLAGS="$CFLAGS -I$z"
  fi
done
LDFLAGS="$LD_FLAGS"
for z in $LIBRARY_PATHS ""; do
  if test -n "$z"; then
    LDFLAGS="$LDFLAGS -L$z"
  fi
done

AC_C_LONG_LONG(AC_DEFINE(HAVE_LONG_LONG,1,[defined if long long data type available]),AC_MSG_ERROR(long long data type not available))
AC_SYS_LARGEFILE
dnl AC_CHECK_LARGEFILE_DEFINES
AC_CHECK_PTHREAD_DEFINES

AC_CHECK_FUNCS(fseeko,,AC_ERROR([function fseeko() is not available]))
AC_CHECK_FUNCS(ftello,,AC_ERROR([function ftello() is not available]))

AC_SEARCH_LIBS(round,m,,AC_ERROR([n library not found]))
AC_SEARCH_LIBS(pthread_create,pthread,,AC_ERROR([pthread library not found]))
if test $ENABLE_ZIP = "yes"; then
  if test $ENABLE_LINK_DYNAMIC = "yes"; then
    AC_SEARCH_LIBS(zlibVersion,z,[HAVE_Z=1;LIBRARIES="$LIBRARIES z";AC_DEFINE(HAVE_Z,1,[z installed])])
  else
    AC_SEARCH_LIBS(zlibVersion,z,[HAVE_Z=1;STATIC_LIBRARIES="$STATIC_LIBRARIES z";AC_DEFINE(HAVE_Z,1,[z installed])])
  fi
fi
if test $ENABLE_BZIP2 = "yes"; then
  if test $ENABLE_LINK_DYNAMIC = "yes"; then
    AC_SEARCH_LIBS(BZ2_bzCompressInit,bz2,[HAVE_BZ2=1;LIBRARIES="$LIBRARIES bz2";AC_DEFINE(HAVE_BZ2,1,[bzip2 installed])])
  else
    AC_SEARCH_LIBS(BZ2_bzCompressInit,bz2,[HAVE_BZ2=1;STATIC_LIBRARIES="$STATIC_LIBRARIES bz2";AC_DEFINE(HAVE_BZ2,1,[bzip2 installed])])
  fi
fi
if test $ENABLE_GCRYPT = "yes"; then
  AC_CHECK_TYPE(gcry_cipher_hd_t,,AC_MSG_WARN([no or wrong version of gcrypt. gcrypt version >=1.2.4 needed. Disabled gcrypt support.]); ENABLE_GCRYPT=no,[#include <gcrypt.h>])
fi
if test $ENABLE_GCRYPT = "yes"; then
  if test $ENABLE_LINK_DYNAMIC = "yes"; then
    AC_SEARCH_LIBS(gcry_check_version,gcrypt,[HAVE_GCRYPT=1;LIBRARIES="$LIBRARIES gcrypt crypto";AC_DEFINE(HAVE_GCRYPT,1,[gcrypt installed])])
  else
    AC_SEARCH_LIBS(gcry_check_version,gcrypt,[HAVE_GCRYPT=1;STATIC_LIBRARIES="$STATIC_LIBRARIES gcrypt crypto";AC_DEFINE(HAVE_GCRYPT,1,[gcrypt installed])])
  fi
  if test $ENABLE_LINK_DYNAMIC = "yes"; then
    AC_SEARCH_LIBS(gpg_strerror,gpg-error,[HAVE_GPG_ERROR=1;LIBRARIES="$LIBRARIES gpg-error";AC_DEFINE(HAVE_GPG_ERROR,1,[GPG error installed])])
  else
    AC_SEARCH_LIBS(gpg_strerror,gpg-error,[HAVE_GPG_ERROR=1;STATIC_LIBRARIES="$STATIC_LIBRARIES gpg-error";AC_DEFINE(HAVE_GPG_ERROR,1,[GPG error installed])])
  fi
fi
if test $ENABLE_FTP = "yes"; then
  if test $ENABLE_LINK_DYNAMIC = "yes"; then
    AC_SEARCH_LIBS(FtpInit,ftp,[HAVE_FTP=1;LIBRARIES="$LIBRARIES ftp";AC_DEFINE(HAVE_FTP,1,[FTP installed])])
  else
    AC_SEARCH_LIBS(FtpInit,ftp,[HAVE_FTP=1;STATIC_LIBRARIES="$STATIC_LIBRARIES ftp";AC_DEFINE(HAVE_FTP,1,[FTP installed])])
  fi
fi
if test $ENABLE_SSH = "yes"; then
  if test $ENABLE_LINK_DYNAMIC = "yes"; then
    AC_SEARCH_LIBS(libssh2_session_init_ex,ssh2,[HAVE_SSH2=1;LIBRARIES="$LIBRARIES ssh2 crypto";AC_DEFINE(HAVE_SSH2,1,[SSH2 installed])])
  else
    AC_SEARCH_LIBS(libssh2_session_init_ex,ssh2,[HAVE_SSH2=1;STATIC_LIBRARIES="$STATIC_LIBRARIES ssh2 crypto";AC_DEFINE(HAVE_SSH2,1,[SSH2 installed])])
  fi
  AC_CHECK_FUNC(libssh2_channel_send_keepalive,AC_DEFINE(HAVE_SSH2_CHANNEL_SEND_KEEPALIVE,1,[SSH2 libssh2_channel_send_keepalive]))
fi
if test $ENABLE_TLS = "yes"; then
  AC_CHECK_TYPE(gnutls_session_t,,AC_MSG_WARN([no or wrong version of gnutls. gnutls version >=2.0.1 needed. Disabled TLS support.]); ENABLE_TLS=no,[#include <gnutls/gnutls.h>])
fi
if test $ENABLE_TLS = "yes"; then
  if test $ENABLE_LINK_DYNAMIC = "yes"; then
    AC_SEARCH_LIBS(gnutls_global_init,gnutls,[HAVE_GNU_TLS=1;LIBRARIES="$LIBRARIES gnutls";AC_DEFINE(HAVE_GNU_TLS,1,[GNU TLS installed])])
  else
    AC_SEARCH_LIBS(gnutls_global_init,gnutls,[HAVE_GNU_TLS=1;STATIC_LIBRARIES="$STATIC_LIBRARIES gnutls";AC_DEFINE(HAVE_GNU_TLS,1,[GNU TLS installed])])
  fi
fi

dnl -------------------------------- misc ------------------------------

AC_DEFINE_UNQUOTED(CONFIG_DIR,"$CONFIG_DIR",[configuration directory])
AC_DEFINE_UNQUOTED(TLS_DIR,   "$TLS_DIR",   [TLS directory])

dnl ------------ autoheader: top/bottom include for config.h -----------

AC_CONFIG_HEADERS(config.h)

dnl no AC_DEFINE should occur after this line!

AH_TOP([
#ifndef __CONFIG__
#define __CONFIG__
])

AH_TOP([

#ifndef offsetof
  #define offsetof(type,member) ((size_t)&((type*)0)->member)
#endif /* offsetof */

])

AH_BOTTOM([#endif /* __CONFIG__ */])

dnl --------------------------------------------------------------------

if test "$prefix" = "NONE"; then
  prefix=/usr/local
fi
INSTALL_DIR=$prefix
if test "$bindir" = "\${exec_prefix}/bin"; then
  if test "$exec_prefix" = NONE; then
    INSTALL_BIN_DIR=$prefix/bin
  else
    INSTALL_BIN_DIR=$exec_prefix
  fi
else
  INSTALL_BIN_DIR=$bindir
fi

AC_CONFIG_FILES(Makefile)

AC_CONFIG_COMMANDS(TCL,
                   [
                    (cd tcl/scanx; ./configure)
                   ]
                  )

AC_CONFIG_COMMANDS(BUILD_NOTE,
                   [echo "";
                    echo "Configuration:";
                    echo "  debug version    : `if test $ENABLE_DEBUG = "yes"; then echo yes; else echo no; fi`";
                    echo "  dynamic linked   : `if test $ENABLE_LINK_DYNAMIC = "yes"; then echo yes; else echo no; fi`";
                    echo "  zip compression  : `if test $HAVE_Z; then echo yes; else echo no; fi`";
                    echo "  bzip2 compression: `if test $HAVE_BZ2; then echo yes; else echo no; fi`";
                    echo "  crypto support   : `if test $HAVE_GCRYPT; then echo yes; else echo no; fi`";
                    echo "  FTP support      : `if test $HAVE_FTP; then echo yes; else echo no; fi`";
                    echo "  SCP/SFTP support : `if test $HAVE_SSH2; then echo yes; else echo no; fi`";
                    echo "  TLS/SSL server   : `if test $HAVE_GNU_TLS; then echo yes; else echo no; fi`";
                   ],
                   ENABLE_DEBUG=$ENABLE_DEBUG
                   ENABLE_LINK_DYNAMIC=$ENABLE_LINK_DYNAMIC
                   HAVE_Z=$HAVE_Z
                   HAVE_BZ2=$HAVE_BZ2
                   HAVE_GCRYPT=$HAVE_GCRYPT
                   HAVE_FTP=$HAVE_FTP
                   HAVE_SSH2=$HAVE_SSH2
                   HAVE_GNU_TLS=$HAVE_GNU_TLS
                  )

AC_OUTPUT

dnl --- end of file ---



